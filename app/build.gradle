apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion rootProject.compileSdkVersion

    defaultConfig {
        applicationId "com.mobiledevpro.apptemplate.new"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        vectorDrawables.useSupportLibrary = true
    }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2g"
    }

    signingConfigs {
        //uses for CI
        config {
            keyAlias 'here alias value'
            keyPassword System.getenv("SOME_APP_KEYPWD")
            storeFile file('here key file name')
            storePassword System.getenv("SOME_APP_KSTOREPWD")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }

    flavorDimensions "prod"
    productFlavors {
        production {
            applicationId defaultConfig.applicationId
            versionCode 1
            versionName "1.0.0"

            //it's for using the same flavors in modules/libraries
            matchingFallbacks = ["production"]
        }

        demo {
            applicationId defaultConfig.applicationId
            versionCode 1
            versionName "1.0.0-demo"

            //it's for using the same flavors in modules/libraries
            matchingFallbacks = ["demo"]
        }
    }

    sourceSets {
        main {
            res.srcDirs = [
                    'src/main/res',
                    'src/main/res/layouts/activity',
                    'src/main/res/layouts/fragment',
                    'src/main/res/layouts/adapter',
                    'src/main/res/layouts/include',
                    'src/main/res/layouts/preference'
            ]
            java.srcDirs = ['src/main/java']
        }
    }

    //generate apk with custom name
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                outputFileName = variant.productFlavors[0].applicationId + "-" +
                        "v" + variant.productFlavors[0].versionName + "_" +
                        "build" + variant.productFlavors[0].versionCode + "-" +
                        variant.buildType.name +
                        ".apk";
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.appcompat
    implementation deps.material
    implementation deps.preference

    implementation deps.coreKtx
    implementation deps.kotlin

    implementation deps.butterknife
    annotationProcessor deps.butterknifeCompiler

    implementation deps.lifecycle
    annotationProcessor deps.lifecycleCompiler

    //commons-ui
    implementation deps.commonsUi

    //Firebase
    implementation deps.firebaseCore
    implementation deps.firebasePerformance
    implementation deps.firebaseAnalytics
    implementation deps.crashlytics

    //memory leak detection in debug
    debugImplementation deps.leakcanaryDebug
    releaseImplementation deps.leakcanaryRelease

    implementation project(":data")
}
apply plugin: 'com.google.gms.google-services'
