// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.rootVersions = [
            "kotlin"        : "1.5.31",
            "gradle"        : "4.2.2",
            "playServices"  : "4.3.10",
            "koin"          : "3.1.2",
            "navigation"    : '2.3.5',
            "firebasePlugin": [
                    "crashlytics": "2.7.1",
                    "performance": "1.4.0"
            ]
    ]

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$rootVersions.gradle"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$rootVersions.kotlin"
        classpath "com.google.gms:google-services:$rootVersions.playServices"
        classpath "com.google.firebase:firebase-crashlytics-gradle:$rootVersions.firebasePlugin.crashlytics"
        classpath "com.google.firebase:perf-plugin:$rootVersions.firebasePlugin.performance"
        classpath "io.insert-koin:koin-gradle-plugin:$rootVersions.koin"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$rootVersions.navigation"


        //it uses to publish 'common-ui' module as library to JitPack.
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

// Gradle Doctor
// https://runningcode.github.io/gradle-doctor/
plugins {
    id("com.osacky.doctor") version "0.7.3"
}

// Gradle Doctor all settings here https://runningcode.github.io/gradle-doctor/configuration/
doctor {
    disallowMultipleDaemons = true
    negativeAvoidanceThreshold = 500
    warnWhenJetifierEnabled = true

    javaHome {
        ensureJavaHomeIsSet = true
        ensureJavaHomeMatches = true
        failOnError.set(true)
    }
}

allprojects {
    repositories {
        google()
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    appVersionName = "1.0.0"
    appVersionCode = 1

    compileSdkVersion = 30
    minSdkVersion = 21
    targetSdkVersion = 30

    versions = [
            "appcompat"       : '1.3.1',
            "preference"      : "1.1.1",
            "material"        : '1.4.0',
            "fragment"        : '1.3.6',
            "activity"        : '1.3.1',
            "constraintLayout": '2.1.0',
            "coreKtx"         : '1.6.0',
            "commonUi"        : "1.1.1",
            "lifecycle"       : '2.3.1',
            "firebase"        : [
                    "bom": "29.0.0"
            ],
            "rx"              : [
                    "android": "2.1.1",
                    "kotlin" : "2.4.0"
            ],
            "room"            : "2.2.4",
            "glide"           : '4.12.0',
            "lottie"          : '4.1.0',
            "retrofit"        : "2.3.0",
            "okhttp"          : "4.2.2",
            "jUnitExt"        : '1.1.3',
            "jUnit"           : '4.13.2',
            "testRunner"      : '1.4.0',
            "espressoCore"    : '3.4.0',
            "mockito"         : '3.12.4',
            "leakcanary"      : '2.7',
            "playCore"        : '1.10.1',
            "playCoreKtx"     : '1.8.1'
    ]

    deps = [
            "appcompat"               : "androidx.appcompat:appcompat:$versions.appcompat",
            "preference"              : "androidx.preference:preference:$versions.preference",
            "material"                : "com.google.android.material:material:$versions.material",
            "constraintLayout"        : "androidx.constraintlayout:constraintlayout:$versions.constraintLayout",
            "coreKtx"                 : "androidx.core:core-ktx:$versions.coreKtx",
            "activity"                : "androidx.activity:activity-ktx:$versions.activity",
            "fragment"                : "androidx.fragment:fragment-ktx:$versions.fragment",

            //Navigation
            "navigationFragment"      : "androidx.navigation:navigation-fragment-ktx:$rootVersions.navigation",
            "navigationUi"            : "androidx.navigation:navigation-ui-ktx:$rootVersions.navigation",

            //Dynamic Feature support
            "navigationDynamic"       : "androidx.navigation:navigation-dynamic-features-fragment:$rootVersions.navigation",
            "playCore"                : "com.google.android.play:core:$versions.playCore",
            "playCoreKtx"             : "com.google.android.play:core-ktx:$versions.playCoreKtx",

            //Firebase
            "firebaseBom"             : "com.google.firebase:firebase-bom:$versions.firebase.bom",
            "firebasePerformance"     : "com.google.firebase:firebase-perf-ktx",
            "crashlytics"             : "com.google.firebase:firebase-crashlytics-ktx",
            "firebaseAnalytics"       : "com.google.firebase:firebase-analytics-ktx",

            //common
            "commonUI"                : "com.github.mobiledevpro:android-kotlin-mvvm-template:$versions.commonUi",

            //LifeCycle
            "viewModel"               : "androidx.lifecycle:lifecycle-viewmodel-ktx:$versions.lifecycle",
            "liveData"                : "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle",
            "saveState"               : "androidx.lifecycle:lifecycle-viewmodel-savedstate:$versions.lifecycle",
            "lifecycleService"        : "androidx.lifecycle:lifecycle-service:$versions.lifecycle",
            "lifecycleCompiler"       : "androidx.lifecycle:lifecycle-compiler:$versions.lifecycle",

            //DI, Koin

            //Koin core features
            "koinCore"                : "io.insert-koin:koin-core:$rootVersions.koin",
            //Koin main features for Android (Scope,ViewModel ...)
            "koinAndroid"             : "io.insert-koin:koin-android:$rootVersions.koin",
            //Koin Java Compatibility
            "koinCompat"              : "io.insert-koin:koin-android-compat:$rootVersions.koin",

            //Rx
            "rxAndroid"               : "io.reactivex.rxjava2:rxandroid:$versions.rx.android",
            "rxKotlin"                : "io.reactivex.rxjava2:rxkotlin:$versions.rx.kotlin",

            //Room
            "room"                    : "androidx.room:room-runtime:$versions.room",
            "roomKtx"                 : "androidx.room:room-ktx:$versions.room",
            "roomCompiler"            : "androidx.room:room-compiler:$versions.room",
            "roomRx"                  : "androidx.room:room-rxjava2:$versions.room",

            //UI libs
            "glide"                   : "com.github.bumptech.glide:glide:$versions.glide",
            "glideCompiler"           : "com.github.bumptech.glide:compiler:$versions.glide",
            "lottie"                  : "com.airbnb.android:lottie:$versions.lottie",

            //Network
            "retrofit"                : "com.squareup.retrofit2:retrofit:$versions.retrofit",
            "retrofitGsonConverter"   : "com.squareup.retrofit2:converter-gson:$versions.retrofit",
            "retrofitRx"              : "com.squareup.retrofit2:adapter-rxjava2:$versions.retrofit",
            "okhttpLoggingInterceptor": "com.squareup.okhttp3:logging-interceptor:$versions.okhttp",

            //testing
            "leakcanaryDebug"         : "com.squareup.leakcanary:leakcanary-android:$versions.leakcanary",
            "testRunner"              : "androidx.test:runner:$versions.testRunner",
            "jUnitExt"                : "androidx.test.ext:junit:$versions.jUnitExt",
            "jUnit"                   : "junit:junit:$versions.jUnit",
            "espressoCore"            : "androidx.test.espresso:espresso-core:$versions.espressoCore",
            "mockito"                 : "org.mockito:mockito-core:$versions.mockito",
            "koinTest"                : "io.insert-koin:koin-test:$rootVersions.koin",
            "roomTesting"             : "androidx.room:room-testing:$versions.room",
    ]

    features = [
            "chatCore": project(":feature:chat_core")
    ]

    core = [
            "navigation": project(":core:navigation"),
            "utils"     : project(":core:utils"),
            "rx"        : project("core:rx"),
            "errors"    : project("core:errors"),
            "database"  : project("core:database")
    ]
}
