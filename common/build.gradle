apply from: "$rootDir/common-android-library.gradle"
apply plugin: 'maven-publish'

def libraryVersion = '1.1.3'
def publishingGroupId = 'com.github.mobiledevpro'
def publishingArtifactId = 'android-kotlin-mvvm-template'

android.namespace = 'com.mobiledevpro.common.ui'

group = publishingGroupId
version = libraryVersion

//read more about publishing to JitPack.io https://github.com/jitpack-io/multi-flavor-lib-demo

android.buildFeatures.dataBinding = true

dependencies {

    api deps.coreKtx
    api deps.appcompat
    api deps.material

    api deps.activity
    api deps.fragment

    //Lifecycle
    api deps.viewModel
    api deps.liveData
    api deps.saveState
    kapt deps.lifecycleCompiler

    //Rx
    implementation deps.rxAndroid

}

//The following code needed to publishing this library to JitPack.io
android.publishing {
    singleVariant('productionRelease') {
        withSourcesJar()
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.productionRelease
                groupId = publishingGroupId
                artifactId = publishingArtifactId
                version = libraryVersion
            }
        }
    }
}